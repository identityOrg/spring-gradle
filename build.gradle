

buildscript {
    ext {
        springBootVersion = '1.3.6.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'ro.isdc.wro4j.gradle:wro4j-gradle-plugin:1.8.0.Beta4'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

ext {
    projectVersion = '1.0'
}

group = 'net.prasenjit.gradle'
version = "${projectVersion}"

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'wro4j'
apply plugin: 'spring-boot'
apply plugin: 'maven'
apply plugin: 'maven-publish'

jar {
    baseName = 'spring-gradle'
    version = "${projectVersion}"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.cloud:spring-cloud-cluster-hazelcast')
    compile('org.springframework.cloud:spring-cloud-cluster-autoconfigure')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.projectlombok:lombok:1.16.6')
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('org.springframework.boot:spring-boot-starter-test')

    compile('org.webjars:bootstrap:3.3.6')
}

webResources {
    bundle ('theme-default') {
        css "webjars/bootstrap/3.3.6/less/bootstrap.less"

        preProcessor 'less4j'
        cssRewriteUrl()
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.SR2"
    }
}

bootRepackage {
    executable = true
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            groupId group
            artifactId 'spring-gradle'
            version "${projectVersion}"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'spring-gradle'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/prasenjit-net/spring-gradle.git'
        publicDownloadNumbers = true
        version {
            name = "${projectVersion}"
            desc = "Gradle Bintray test for version ${projectVersion}"
            vcsTag = "${projectVersion}"
        }
    }
}

bintrayUpload.dependsOn build
publish.dependsOn bintrayUpload